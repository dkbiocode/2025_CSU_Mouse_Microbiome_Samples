configfile: "config.yaml"
DATA_DIR = config["esdaile_data_dir"]
WORK_DIR = config["scratch_data_dir"]
BACKBONE = config["esdaile_microbiome_taxonomy"] + "/" + config["esdaile_greengenes_rel"]
SEPP_REF = config["esdaile_microbiome_taxonomy"] + "/" + config["esdaile_sepp_rel"]

rule all:
	input:
		WORK_DIR + "/03_mouse_fecal_samples_taxonomy_greengenes.qzv",
		WORK_DIR + "/03_table_mouse_fecal_samples_filter_taxa_samples_3_reads_50_greengenes.qzv",
		WORK_DIR + "/03_table_mouse_fecal_samples_filter_taxa_samples_3_reads_100_greengenes.qzv",
		WORK_DIR + "/03_mouse_fecal_samples_taxonomy_barplot_samples_3_reads_50_greengenes.qzv",
		WORK_DIR + "/03_mouse_fecal_samples_taxonomy_barplot_samples_3_reads_100_greengenes.qzv",
		WORK_DIR + "/03_mouse_fecal_sampless_alpha_rarefaction_curves_samples_3_reads_50_greengenes.qzv",
		WORK_DIR + "/03_mouse_fecal_sampless_alpha_rarefaction_curves_samples_3_reads_100_greengenes.qzv",
		directory(WORK_DIR + "/03_mouse_fecal_samples_beta_div_samples_3_reads_50_greengenes"),
		directory(WORK_DIR + "/03_mouse_fecal_samples_beta_div_samples_3_reads_100_greengenes")


rule classify_features:
	input:
		refseqs=f"{WORK_DIR}/02_repSeqs_mouse_fecal_samples.qza",
		backbone=f"{BACKBONE}"
	output:
		f"{WORK_DIR}/03_mouse_fecal_samples_taxonomy_greengenes.qza"
	threads:
		workflow.cores
	shell:
		"qiime feature-classifier classify-sklearn \
		--i-reads {input.refseqs} \
		--i-classifier {input.backbone} \
		--p-n-jobs {threads} \
		--o-classification {output}"

rule visualize_features:
	input:
		WORK_DIR + "/03_mouse_fecal_samples_taxonomy_greengenes.qza"
	output:
		WORK_DIR + "/03_mouse_fecal_samples_taxonomy_greengenes.qzv"
	shell:
		"qiime metadata tabulate \
		 --m-input-file {input} \
		 --o-visualization {output}"


rule filter_mito_chloro:
	input:
		table=f"{WORK_DIR}/02_table_mouse_fecal_samples.qza",
		taxonomy=f"{WORK_DIR}/03_mouse_fecal_samples_taxonomy_greengenes.qza"
	output:
		f"{WORK_DIR}/03_table_mouse_fecal_samples_filter_taxa_greengenes.qza"
	shell:
		"qiime taxa filter-table \
		--i-table {input.table} \
		--i-taxonomy {input.taxonomy} \
		--p-exclude mitochondria,chloroplast \
		--o-filtered-table {output}"

rule filter_samples_read_number:
	input:
		f"{WORK_DIR}/03_table_mouse_fecal_samples_filter_taxa_greengenes.qza"
	output:
		f"{WORK_DIR}/03_table_mouse_fecal_samples_filter_taxa_samples_greengenes.qza"
	shell:
		"qiime feature-table filter-samples \
		 --i-table {input} \
		 --p-min-frequency 1000 \
		 --o-filtered-table {output}"


rule filter_sequences_read_number_3_100:
	input:
		f"{WORK_DIR}/03_table_mouse_fecal_samples_filter_taxa_samples_greengenes.qza"
	output:
		f"{WORK_DIR}/03_table_mouse_fecal_samples_filter_taxa_samples_3_reads_100_greengenes.qza"
	shell:
		"qiime feature-table filter-features \
		--i-table {input} \
		--p-min-frequency 100 \
		--p-min-samples 3 \
		--o-filtered-table {output}"


rule filter_sequences_read_number_3_50:
	input:
		f"{WORK_DIR}/03_table_mouse_fecal_samples_filter_taxa_samples_greengenes.qza"
	output:
		f"{WORK_DIR}/03_table_mouse_fecal_samples_filter_taxa_samples_3_reads_50_greengenes.qza"
	shell:
		"qiime feature-table filter-features \
		--i-table {input} \
		--p-min-frequency 50 \
		--p-min-samples 3 \
		--o-filtered-table {output}"

rule visualize_filtered:
	input:
		filt=WORK_DIR + "/03_table_mouse_fecal_samples_filter_taxa_{filter}_greengenes.qza",
		meta="metadata.txt"
	output:
		WORK_DIR + "/03_table_mouse_fecal_samples_filter_taxa_{filter}_greengenes.qzv"
	shell:
		"qiime feature-table summarize \
		--i-table {input.filt} \
		--m-sample-metadata-file {input.meta} \
		--o-visualization {output}"

rule taxonomy_barplot:
	input:
		table=WORK_DIR + "/03_table_mouse_fecal_samples_filter_taxa_{filter}_greengenes.qza",
		taxa=f"{WORK_DIR}/03_mouse_fecal_samples_taxonomy_greengenes.qza",
		meta="metadata.txt"
	output:
		WORK_DIR + "/03_mouse_fecal_samples_taxonomy_barplot_{filter}_greengenes.qzv"
	shell:
		"qiime taxa barplot \
		--i-table {input.table} \
		--i-taxonomy {input.taxa} \
		--m-metadata-file {input.meta} \
		--o-visualization {output}"


rule make_tree:
	input:
		repseqs=f"{WORK_DIR}/02_repSeqs_mouse_fecal_samples.qza",
		backbone=SEPP_REF
	output:
		tree=WORK_DIR + "/03_mouse_fecal_samples_tree_greengenes.qza",
		placements=f"{WORK_DIR}/03_mouse_fecal_samples_tree_placements_greengenes.qza"
	threads:
		workflow.cores  
	shell:
		"qiime fragment-insertion sepp \
		--i-representative-sequences {input.repseqs} \
		--i-reference-database {input.backbone} \
		--p-threads {threads} \
		--o-tree {output.tree} \
		--o-placements {output.placements}"


rule rarefaction:
	input:
		filt=WORK_DIR + "/03_table_mouse_fecal_samples_filter_taxa_{filter}_greengenes.qza",
		meta="metadata.txt"
	output:
		WORK_DIR + "/03_mouse_fecal_sampless_alpha_rarefaction_curves_{filter}_greengenes.qzv"
	shell:
		"qiime diversity alpha-rarefaction \
		--i-table {input.filt} \
		--m-metadata-file {input.meta} \
		--o-visualization {output} \
		--p-min-depth 10 \
		--p-max-depth 6000"

rule diversity:
	input:
		table=WORK_DIR + "/03_table_mouse_fecal_samples_filter_taxa_{filter}_greengenes.qza",
		tree=WORK_DIR + "/03_mouse_fecal_samples_tree_greengenes.qza",
		meta="metadata.txt"
	output:
		directory(WORK_DIR + "/03_mouse_fecal_samples_beta_div_{filter}_greengenes")
	threads:
		workflow.cores  
	shell:
		"qiime diversity core-metrics-phylogenetic \
		--i-table {input.table} \
		--i-phylogeny {input.tree} \
		--m-metadata-file {input.meta} \
		--p-sampling-depth 1300 \
		--p-n-jobs-or-threads {threads} \
		--output-dir {output}"

